<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet 
	version='1.0'
	xmlns:xsl='http://www.w3.org/1999/XSL/Transform'
	xmlns:xhtml="http://www.w3.org/1999/xhtml"
	xmlns:xforms="http://www.w3.org/2002/xforms"
    xmlns:ts="http://www.w3c.org/MarkUp/Forms/XForms/Test/11"
>
	<!--
		
		Transformation to convert (with Xalan) XML XF test suite results generated by ? into HTML table format.
		
	-->
	
	<!--
		Ouput options.
	-->
	<xsl:output 
		method="xml" 
		encoding="UTF-8" 
		indent="yes" 
		omit-xml-declaration="no" 
		xalan:indent-amount="2" 
		xmlns:xalan="http://xml.apache.org/xslt"
	/>
	
	<!-- 
		This seems to be dual purpose, indicating the browser being tested and also a directory name. Defined in build.xml.
	-->
    <xsl:param name="dir"></xsl:param>

	<!-- 
		Generate the test suite results summary document
	-->
	<xsl:template match="/ts:testSuite">
		<html 
			xmlns="http://www.w3.org/1999/xhtml"  
	    	xmlns:ts="http://www.w3c.org/MarkUp/Forms/XForms/Test/11"
	    	exclude-result-prefixes="ts"
		>
		    <head>
		        <title>XForms Results Table</title>
		        <link rel="stylesheet" href="../../Results.css" type="text/css"></link>
		        <link rel="stylesheet" href="../../TestSuite11.css" type="text/css"></link>       
		    </head>
		    <body>
		        <h1 class="title">XForms 1.1 Test Suite Implementation Status for Ubiquity-XForms</h1>
		        <h2 class="subtitle">XForms 1.1 Test -- May 29, 2009</h2>
		        <h3>This report describes the results from testing the XForms 1.1 Test Suite with Ubiquity-XForms.</h3>
		    
		        <h2 class="subtitle">Legend</h2>
		       
		        <table cellpadding="2" cellspacing="1" border="1" >
		            <tr>
		                <th class="testCaseNameTitle" width="80">Type</th>
		                <th class="testCaseNameTitle" width="300">Description</th>
		            </tr>
		            <tr>
		                <td class="green">Passed</td>
		                <td>The test passed when it was run manually.</td>
		            </tr>
		            <tr>
		                <td class="red">Failed</td>
		                <td>The test failed when it was run manually.</td>
		            </tr>
		        	<tr>
		        		<td class="orange">Failed</td>
		        		<td>The OPTIONAL test failed when it was run manually.</td>
		        	</tr>
		            <tr>
		                <td class="white">Unknown</td>
		                <td>The test was not run, or an unknown result occurred.</td>
		            </tr>
		        </table>
		    	
		    	<xsl:apply-templates select="ts:specChapter/ts:statusSummary" />
		    	
		        <h2 class="subtitle">Results Table</h2>		        
		        <table cellpadding="2" cellspacing="1" border="1">
		            <tr class="heading">
		                <td class="inner130">Test Case Number</td>
		                <td class="innerLongCell">Test Case Name</td>
		                <td class="innerTitle"><xsl:value-of select="$dir"></xsl:value-of></td>
		            </tr>
		        	<tr class="chapter">
						<td colspan="3">
							<xsl:value-of select="ts:specChapter/@chapterName" />
							<xsl:value-of select="ts:specChapter/@chapterTitle" />
						</td>
					</tr>
		        	<xsl:apply-templates select="ts:specChapter/ts:testCase" />
		        </table>
		    </body>
		</html>
	</xsl:template>
	
	<!--  
		Create a heading type row for the results table with the chapter name and title.
	-->
	<!-- xsl:template match="ts:specChapter">
		<xsl:apply-templates />    
	</xsl:template -->

	<!-- 
		Create table row for each chapter test case.
	-->
	<xsl:template match="ts:testCase">
		<tr class="outer">
			<td class="inner130"><xsl:value-of select="ts:testCaseName" /></td>
            <td class="innerLongCell"><xsl:value-of select="ts:testCaseDescription" /></td>
			
			<xsl:choose>
				<xsl:when test="ts:testCaseStatus = 'Failed' and ts:testCaseRequired = 'true'">
					<td class="innerCellFailed"><xsl:value-of select="ts:testCaseStatus" /></td>
				</xsl:when>
				<xsl:when test="ts:testCaseStatus = 'Failed' and ts:testCaseRequired = 'false'">
					<td class="innerCellFailedOptional"><xsl:value-of select="ts:testCaseStatus" /></td>
				</xsl:when>
				<xsl:when test="ts:testCaseStatus = 'Passed'">
					<td class="innerCellPassed"><xsl:value-of select="ts:testCaseStatus" /></td>
				</xsl:when>
				<xsl:otherwise>
					<td><xsl:value-of select="ts:testCaseStatus" /></td>
				</xsl:otherwise>
			</xsl:choose>            
        </tr>
	</xsl:template>

	<!-- 
		Show the chapter results summary info
	-->
	<xsl:template match="ts:statusSummary">
		<h2 class="subtitle">Chapter Results Summary</h2>

		<table cellpadding="2" cellspacing="1" border="1" width="500">
			<xsl:variable name="testCasesNormative" select="../ts:testCase[ts:testCaseNormative = 'true']"/>
			<tr>
				<td colspan="2"><strong><xsl:value-of select="count($testCasesNormative)" /> normative tests</strong></td>
			</tr>
			<tr>
				<td>Passed</td>
				<td><xsl:value-of select="count($testCasesNormative[ts:testCaseStatus = 'Passed'])" /></td>
			</tr>
			<tr>
				<td>Failed</td>
				<td><xsl:value-of select="count($testCasesNormative[ts:testCaseStatus = 'Failed'])" /></td>
			</tr>
			<tr>
				<td>Unknown</td>
				<td>not calculated</td>
			</tr>
			<xsl:variable name="testCasesBasic" select="../ts:testCase[ts:testCaseBasic = 'true']"/>
			<tr>
				<td colspan="2"><strong><xsl:value-of select="count($testCasesBasic)" /> basic tests</strong></td>
			</tr>
			<tr>
				<td>Passed</td>
				<td><xsl:value-of select="count($testCasesBasic[ts:testCaseStatus = 'Passed'])" /></td>
			</tr>
			<tr>
				<td>Failed</td>
				<td><xsl:value-of select="count($testCasesBasic[ts:testCaseStatus = 'Failed'])" /></td>
			</tr>
			<tr>
				<td>Unknown</td>
				<td>not calculated</td>
			</tr>
		</table>
	</xsl:template>

    <!-- 
    	Ignore following elements in input
    -->   
    <xsl:template match="ts:profile"/> 
</xsl:stylesheet>